buildscript {
    repositories {
      maven {
        url "https://plugins.gradle.org/m2/"
      }
      mavenCentral()
    }
    dependencies {
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: '2.0.3.RELEASE'
        classpath group: 'gradle.plugin.com.palantir.gradle.docker', name: 'gradle-docker', version: '0.13.0'
    }
}

ext { springBootVersion = '2.0.3.RELEASE' }

apply plugin: 'com.palantir.docker'
apply plugin: "jacoco"
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

// This is used as the docker image prefix (org)
group = 'tjaroszewski'

bootJar {
    baseName = project.name
    version =  '0.1.0'
}

docker {
    dependsOn build
    name "${project.group}/${bootJar.baseName}"
    files bootJar.archivePath
    buildArgs(['JAR_FILE': "${bootJar.archiveName}"])
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencyManagement {
	imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
}

dependencies {
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-jersey'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
	compile group: 'io.swagger', name: 'swagger-jersey2-jaxrs', version:'1.5.13'
	compile group: 'org.hsqldb', name: 'hsqldb', version: '2.4.1'
	compile group: 'com.leandronunes85.jax-rs', name: 'etag-response-filter', version:'1.0'
	
	testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
	testCompile group: 'junit', name: 'junit', version: '4.+'
}

jacocoTestReport {
    reports {
        xml.enabled false
        html.enabled true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

check.dependsOn jacocoTestReport

